---
layout: post
title:  "C/C++逆向二、算术运算"
date:   2018/8/7 22:00:00 
categories: 逆向工程
tag: 算术运算
---

# 常见的编译器优化技巧

- 常量折叠

 在编译期可以确定的两个常量之间的运算，会直接使用计算结果。
 
- 常量传播

 从初始化常量到引用都没有重写，引用时直接则直接使用常量。

- 减少变量
 
 没有任何输入输出的变量，直接删掉。

- 复写传播

 从初始化为变量或变量表达式到引用没有重写，则在引用时直接使用变量或变量表达式。

- 代数/逻辑恒等式

 在数学、逻辑上使用相等的，直接化简。例如`a * 0`化简为0，`a or 1`化简为1。

- 剪枝优化

 条件语句中的不可达分枝直接删掉。

- 强度削弱

 使用代价小的指令代替代价大的指令，例如用加法、移位代替乘法，用乘法、移位代替除法。

- 代码外提

 不同分枝中的相同代码，提到分枝外执行。

- 流水线优化

 因为CPU工作时分为不同阶段，故可以同时执行不同执行的不同阶段。但如果两条指令要对同一寄存器或地址进行写操作，那么便不可同时执行。故编译器会尽可能在不影响程序逻辑的情况下，通过调换指令的顺序，来使CPU可以同时执行多条指令。

---

# 加法的逆向

 直接使用add指令。

---

# 减法的逆向

 大多数直接使用sub指令。但VS Release版在减去常量时，会使用加补码来优化。

---

# 乘法的逆向

 因为32位无符号乘法指令mul和单操作数的有符号乘法指令imul的结果为64位edx.eax，而C语言中的乘法结果仍然为32位，故不管是有符号数相乘，还是无符号数相乘，都使用两操作数或三操作数的有符号乘法指令imul或者逻辑左移指令shl。

  每段代码的最后一条指令`push xxx`仅为表明结果在哪个寄存器中，不一定会有此条指令。

 1. 变量m * 常量非2的n次幂正数c : 使用三操作数的imul
        
        imul reg, m, c
        push reg

 2. 变量m * 常量2的n次幂正数 : 使用逻辑左移指令shl
        
        mov reg, m
        shl reg, n
        push reg

 3. 变量m * 变量n : 使用两操作数的imul

        mov reg, m
        imul reg, n
        push reg

 4. 变量m * 常量负数 : 将负号提出，最先或最后计算，其余与正数相同

        mov reg, m
        neg reg

 5. 变量m * 常量非2的n次幂正数c 的特殊可优化情况 : 使用lea指令
 
        lea eax, [eax + eax * 2] ; eax = eax * 3
        lea eax, [eax + eax * 4] ; eax = eax * 5
        lea eax, [eax + eax * 2]
        add eax, eax             ; eax = eax * 6
        lea ecx, [eax * 8]
        sub ecx, eax             ; ecx = eax * 7
        lea eax, [eax + eax * 8] ; eax = eax * 9
        lea eax, [eax + eax * 4]
        add eax, eax             ; eax = eax * 10
        lea edx, [eax + eax * 4]
        lea eax, [eax + edx * 2] ; eax = eax * 11 gcc, imul vs
        lea eax, [eax + eax * 2]
        shl eax, 2               ; eax = eax * 12
        lea     edx, [eax+eax*2]
        lea     eax, [eax+edx*4] ; eax = eax * 13 gcc, imul vs
        lea ecx, [eax * 8]
        seb ecx, eax
        add ecx, ecx             ; eax = eax * 14 vs, gcc imul
        ......        

---

# 除法

 从原理上讲，除法和取余可以算是逆向中最复杂的部分了，但是如果样本是正常的商业样本，要识别还是不难的。这里开始只陈述结论，有兴趣的可以看后面的原理部分，涉及一些数学推理。

 当被除数、除数中有一个为无符号数时，那么便按照无符号数计算。

 1. 无符号变量m / 无符号变量n

        mov eax, m
        xor edx, edx
        div n
        push eax

 2. 有符号变量m / 有符号变量n

        mov eax, m
        cdq
        idiv n
        push eax

 3. 无符号变量m / 常量2的n次幂

        mov reg, m
        shr reg, n
        push reg

 4. 有符号变量m / 常量2的n次幂正数

    VS Debug与VS Release版:

        mov eax, m
        cdq              ; 取符号位
        and edx, 2^n - 1
        add eax, edx
        sar eax, n
        push eax

    GCC版:

        mov reg1, m
        mov reg2, reg1
        sar reg2, 31     ; 取符号位
        and reg2, 2^n - 1
        add reg2, reg1
        sar reg2, n
        push reg2

 5. 有符号变量m / 常量2的n次幂负数

    在4的基础上多一条neg指令即可。

 6. 无符号变量m / 常量非2的n次幂c

    VS Debug版:使用idiv指令

    VS Release版与GCC版:

    这里的优化需要用到一个叫做MagicNumber的概念，使M=2^n/c(其中n值为编译器在编译时算出，最小为32，算出n的算法详见第9条)，则有m/c=m*M/2^n。

    M不需要进位时:

        mov eax, M
        mul m
        shr edx, n - 32
        push edx ; 还原时，c = 2^n / M 向上取整

    M需要进位时:(例如c = 7)

        mov reg, m
        mov eax, M
        mul reg       ; C语言语法不可能直接产生单操作数的乘法，故出现了只有可能是除法优化或者是内联汇编
        sub reg, edx
        shr reg, 1
        add reg, edx
        shr reg, 2    ; 简介记法："乘减移加移"
        push reg      ; 还原时，c = 2^(32+1+2) / (M + 2^32) 向上取整

 7. 有符号变量m / 常量非2的n次幂c正数

    VS Debug版:使用div指令

    M > 0时:

    VS Release版:

        mov eax, M
        imul m
        sar edx, n - 32
        mov reg, edx     ;
        shr reg, 31      ;
        add reg, edx     ; 此三条指令作用为下整转上整
        push reg         ; 还原时，c = 2^n / M

    GCC版:

        mov eax, M
        imul m
        sar edx, n -32
        sar eax, 31      ;
        sub edx, eax     ; 此两条指令作用为下整转上整
        push reg         ; 还原时，c = 2^n / M

    M < 0时:

    VS Release版:

        mov eax, M
        imul m
        add edx, m        ; 调整
        sar edx, n - 32
        mov reg, edx      ;
        shr reg, 31       ;
        add reg, edx      ; 此三条指令作用为下整转上整
        push reg          ; 还原时，c = 2^n / M

    GCC版:

        mov ecx, m
        mov ebx, M
        mov eax, ecx
        imul ebx
        add edx, ecx      ; 调整
        sar edx, n - 32
        sar ecx, 31       ; 
        sub edx, ecx      ; 此两条指令作用为下整转上整
        push reg          ; 还原时，c = 2^n / M

 8. 有符号变量m / 常量非2的n次幂c负数

    在7的基础上多一条neg指令即可。但此时仍可继续优化。

    VS Debug版:使用idiv指令

    VS Release版:

    M < 0时:

        mov eax, M
        imul m
        sar edx, n - 32
        mov reg, edx     ;
        shr reg, 31      ;
        add reg, edx     ; 此三条指令作用为下整转上整
        push reg         ; 还原时，|c| = 2^n / (~M + 1)

    M > 0时:

        mov eax, M
        imul m
        sub edx, m        ; 调整
        sar edx, n - 32
        mov reg, edx      ;
        shr reg, 31       ;
        add reg, edx      ; 此三条指令作用为下整转上整
        push reg          ; 还原时，|c| = 2^n / (~M + 1)

    GCC版:

     与7相比语句8处edx与ecx互换了。

     M > 0时:

        mov ecx, m
        mov ebx, M
        mov eax, ecx
        imul ebx
        add edx, ecx
        sar edx, n - 32
        sar ecx, 31       ; 
        sub ecx, edx      ; 语句8
        push ecx          ; 还原时，|c| = 2^n / M

    M < 0时:

        mov ecx, m
        mov eax, ecx
        imul M
        add edx, ecx
        sar edx, n - 32
        sar ecx, 31       ;
        sub ecx, edx      ; 语句8
        push ecx          ; 还原时，|c| = 2^n / M 

 9. MagicNumber的获取方法

    待续。

 10. 除法优化的原理

    待续。

 11. 常用除数的MagicNumber表

    除数 | 无符号数的M | 有符号正数的M | 有符号负数的M
    :-:|:-:|:-:|:-:
    3 | 0AAAAAAABh | 55555556h | 55555555h
    5 |
    6 |
    7 |
    9 |
    10 |
    11 |
    12 |
    13 |
    14 |
    15 |
    17 |
    18 |
    19 |
    20 |
    21 |
    22 |
    23 |
    24 |
    25 |
    26 |
    27 |
    28 |
    29 |
    30 |
    31 |

---

# 取模

在C语言中，取模(%)与数学意义上的取模不同，实际上是数学意义上的取余，即a % b = a - a / b * b，其中a / b是向0方向取整(数学上取模此时是向下取整)，于是结果总是和被除数符号一致。

 1. 变量m % 变量n

    直接使用div、idiv运算后，取edx中的值(eax中的值为商)。

 2. 无符号数m % 常量2的n次幂

    VS Debug版:使用div后取edx中的值。

    VS Release版与GCC版:

        mov reg, m
        and reg, 2^n - 1
        push reg

 3. 无符号数m % 常量非2的n次幂c

    VS Debug版:使用div后取edx中的值。

    VS Release版与GCC版:使用r = a - a / b * b公式

        mov reg1, m
        div c
        mov reg2, eax   ; 此处仅代指，实际使用的是除法优化后的算法
        mul reg3, reg2  ; 此处仅代指，实际使用的是乘法优化后的算法
        sub reg1, reg3
        push reg1

 4. 有符号数m % 常量2的n次幂

    VS Debug版:使用div后取edx中的值。

    VS Release版:正数使用jns优化

            mov reg, m
            and reg, (2^n - 1) | 80000000h
            jns label
            dec reg
            or reg, ~(2^n - 1) | 80000000h
            inc reg
        lebel:

    负数优化:

        mov eax, m
        cdq
        xor eax, edx
        sub eax, edx
        and eax, 2^n - 1
        xor eax, edx
        sub eax, edx
        push eax

    GCC版正负优化相同:

        mov eax, m
        cdq
        shr edx, 32 - n
        add eax, edx
        and eax 2^n - 1
        sub eax, edx
        push eax

 5. 有符号数m % 常量非2的n次幂c

    VS Debug版:使用idiv后取edx中的值。

    VS Release版与GCC版:使用r = a - a / b * b公式优化。
